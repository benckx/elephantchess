plugins {
    id "com.github.ben-manes.versions" version "0.52.0"
    id "org.jetbrains.kotlin.jvm" version "$kotlinVersion"
    id "com.adarshr.test-logger" version "4.0.0"
    id 'maven-publish'
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'com.adarshr.test-logger'
    apply plugin: 'maven-publish'

    kotlin {
        jvmToolchain {
            languageVersion.set(JavaLanguageVersion.of("11"))
        }
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
        testImplementation "io.github.oshai:kotlin-logging-jvm:$loggingVersion"
        testImplementation "ch.qos.logback:logback-classic:$logbackVersion"
        testImplementation "org.jetbrains.kotlin:kotlin-test"
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
    }

    test {
        useJUnitPlatform()
        minHeapSize = '1G'
        maxHeapSize = '1G'
    }
}

project(":engine-api") {
    dependencies {
        implementation project(":xiangqi-core")
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinxCoroutinesVersion"
        implementation "io.github.oshai:kotlin-logging-jvm:$loggingVersion"
        implementation "ch.qos.logback:logback-classic:$logbackVersion"
    }
}

project(":xiangqi-core") {
    dependencies {
        testImplementation project(":xiangqi-core-test-utils")
    }
}
